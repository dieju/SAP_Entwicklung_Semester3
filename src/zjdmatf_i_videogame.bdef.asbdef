managed implementation in class zbp_jdmatf_i_videogame unique;
strict ( 1 );

define behavior for ZJDMATF_I_Videogame alias Videogame
persistent table ZJDMATFVIDEOGAME
lock master
authorization master ( instance )
//etag master <field_name>
{
  /* CUD-Methods */
  create;
  update;
  delete ( features : instance );
  association _Rentals { create; }

  /* Field Controls */
  field ( readonly, numbering : managed ) VideogameUuid;
  field ( readonly ) ItemId, AverageRatingCalculated, Status;
  field ( mandatory: create ) Title, PublishingYear, GameSystem, Genre;

  /* Actions, Validations and Determinations */
  determination determineItemId on save { create; }
  determination determineStatus on save { create; }
  validation validatePublishingYear on save { create; update; field PublishingYear; }
  validation validateGenre on save { create; update; field Genre; }
  validation validateGameSystem on save { create; update; field GameSystem; }

  mapping for zjdmatfvideogame corresponding
  {
      VideogameUuid = videogame_uuid;
      ItemId = item_id;
      Genre = genre;
      Title = title;
      GameSystem = game_system;
      PublishingYear = publishing_year;
      Status = status;
  }

}

define behavior for ZJDMATF_I_Rental alias Rental
persistent table ZJDMATFRENTAL
lock dependent by _Videogame
authorization dependent by _Videogame
//etag master <field_name>
{
  /* CUD-Methods */
  update;
  delete ( features : instance );
  field ( readonly ) VideogameUuid;
  association _Videogame;

  /* Actions, Validations and Determinations */
  action return_videogame;

  /* Field Controls */
  field ( readonly, numbering : managed ) RentalUuid;
  field ( readonly) ProcessNumber, StartDate, ReturnDate, RentalCharge, CukyField, RentalStatus;
  field ( readonly: update) CustomerUuid;
  field ( mandatory: create) CustomerUuid, Rating;

  /* Actions, Validations and Determinations */
  determination determineProcessNumber on save { create; }
  determination determineStartDate on save { create; }
  determination determineStatus on save { create; } // + Prüfung, ob Videogame erstellt werden kann (wenn Status = 'L', dann nicht möglich)
  determination determineRating on save { create; }
  validation validateRating on save { create; update; field Rating; }


  /* Mapping */
  mapping for zjdmatfrental corresponding
  {
      RentalUuid = rental_uuid;
      CustomerUuid = customer_uuid;
      VideogameUuid = videogame_uuid;
      ProcessNumber = process_number;
      StartDate = start_date;
      ReturnDate = return_date;
      RentalCharge = rental_charge;
      CukyField = cuky_field;
      Rating = rating;
      RentalStatus = rental_status;
  }
}